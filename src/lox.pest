program = _{SOI ~ declaration* ~ EOI}

declaration = _{
    classDecl
  | funDecl
  | varDecl
  | statement
}

classDecl =  {"class" ~ identifier ~ super_class ~ "{" ~ function* ~ "}" }
super_class = { ("<" ~ identifier)? }
funDecl   =  {"fun" ~ function }
varDecl   =  {"var" ~ identifier ~ ("=" ~ expression)? ~ ";"}

statement = _{
    forStmt
  | ifStmt
  | printStmt
  | returnStmt
  | whileStmt
  | block
  | exprStmt
}

exprStmt   =  {expression ~ ";"}
forStmt    =  {
    "for" ~ "(" ~ for_init ~ for_cond ~ ";" ~ for_step ~ ")" ~ statement
}
for_init = { varDecl|exprStmt|";" }
for_cond = { expression? }
for_step = { expression? }
ifStmt     =  {
    "if" ~ "(" ~ expression ~ ")" ~ statement ~ ("else" ~ statement)?
}
printStmt  =  {"print" ~ expression ~ ";"}
returnStmt =  {"return" ~ expression? ~ ";"}
whileStmt  =  {"while" ~ "(" ~ expression ~ ")" ~ statement}
block      =  {"{" ~ declaration* ~ "}"}

expression =  _{assignment}

assignment =  {
    call ~ "=" ~ assignment | 
    logic_or
}

logic_or         =  {logic_and ~ ("or" ~ logic_and)*}
logic_and        =  {equality ~ ("and" ~ equality)*}
equality_op      =  {"!="|"=="}
equality         =  {comparison ~ (equality_op ~ comparison)*}
comparison_op    =  {">="|">"|"<="|"<"}
comparison       =  {term ~ (comparison_op ~ term)*}
term             =  {factor ~ (term_op ~ factor)*}
term_op          =  {"-"|"+"}
factor           =  {unary ~ (factor_op ~ unary)*}
factor_op        =  {"/"|"*"}
unary            =  {unary_op ~ unary|call}
unary_op         =  {"!"|"-"}
call             =  {primary ~ ("(" ~ arguments ~ ")"|"." ~ identifier)*}
primary          =  {
    primary_keywords
  | number
  | string
  | super_dot ~ identifier
  | "(" ~ expression ~ ")"
  | identifier
}
super_dot = { "super" ~ "." }
primary_keywords =  {
    "true"
  | "false"
  | "nil"
  | "this"
}
function         =  {identifier ~ "(" ~ parameters ~ ")" ~ block}
parameters       =  {(identifier ~ ("," ~ identifier)*)?}
arguments        =  {(expression ~ ("," ~ expression)*)?}
number           = @{digit+ ~ ("." ~ digit+)?}
string           = @{"\"" ~ (!("\"") ~ ANY)* ~ "\""}
identifier       = @{alpha ~ (alpha|digit)*}
alpha            =  {'a' .. 'z'|'A' .. 'Z'|"_"}
digit            =  {'0' .. '9'}

WHITESPACE = _{" "|"\t"|"\r"|"\n"}
COMMENT    = _{"//" ~ (!("\n") ~ ANY)*}

